template header
number
count
timespan
sourceIPRegex
severityParam
facilityParam
messageRegex1
messageRegex2

package rules;

import timejts.SIEMCentre.model.Log;
import timejts.SIEMCentre.model.Alarm;
import timejts.SIEMCentre.model.RaisedAlarm;
import timejts.SIEMCentre.model.Facility;
import timejts.SIEMCentre.model.Severity;
import java.util.Date;
import timejts.SIEMCentre.model.AlarmType;

template "Exceeded number of requests"

rule "ENOR rule_@{number}"
    when
        $log: Log(severity == Severity.@{severityParam}, facility == Facility.@{facilityParam}, $severity: severity, $facility: facility)
        Number(intValue >= @{count}) from accumulate(
           $l: Log(
               this != $log,
               this after[0s, @{timespan}s] $log,
               severity == Severity.@{severityParam},
               facility == Facility.@{facilityParam}
           ),
           count($l)
        )
    then
        System.out.print(drools.getRule().getName());
        RaisedAlarm raisedAlarm = new RaisedAlarm();
        raisedAlarm.setTime(new Date());
        raisedAlarm.setAlarmType(AlarmType.EXCEEDED_NUMBER_OF_REQUESTS);
        raisedAlarm.setSeverity(Severity.@{severityParam});
        raisedAlarm.setFacility(Facility.@{facilityParam});
        insert(raisedAlarm);
end

end template
